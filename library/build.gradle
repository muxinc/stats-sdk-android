plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'com.mux.gradle.android.mux-android-distribution'
}

android {
  compileSdk 34

  namespace "com.mux.core_android"

  defaultConfig {
    minSdk 16
    //noinspection EditedTargetSdkVersion
    targetSdk 34

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds just("android")
  groupIds just("com.mux.stats.sdk.muxstats")
  publicReleaseIf releaseIfCmdFlag("publicRelease")

  dokkaConfig {
    moduleName = "Mux Data SDK for Media3, Base"
    footer = "(c) " + new Date().format("yyyy") + " Mux, Inc. Have questions or need help?" +
            " Contact support@mux.com"
  }

  pom {
    description "Supporting library for Mux Data integrations"
    inceptionYear = "2022"
    url = "https://github.com/muxinc/stats-sdk-android"
    organization {
      name = "Mux, Inc"
      url = "https://www.mux.com"
    }
    developers {
      developer {
        email = "support@mux.com"
        name = "The player and sdks team @mux"
        organization = "Mux, inc"
      }
    }
  }

  // TODO: This is kinda clumsy, packageJavadocs should be a function not a property probably
  packageJavadocs = releaseOnTag().call()
  packageSources = true
  publishIf { it.containsIgnoreCase("release") }
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }
}

dependencies {
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"

  api "com.mux:stats.muxcore:8.1.0"

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'androidx.test.ext:junit:1.1.5'
  testImplementation "io.mockk:mockk:1.13.9"
  // hm, seems like they got rid of API 16 at some point, so we'd have to handle that.
  //   Robolectric doesn't need updated for anything in particular tho, test deps aren't exported
  //noinspection GradleDependency
  testImplementation 'org.robolectric:robolectric:4.8.1'
}